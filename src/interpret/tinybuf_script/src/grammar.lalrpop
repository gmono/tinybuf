use crate::ast::*;
grammar;

match {
    "let",
    "print",
    "reg",
    "operator",
    "return",
    "list",
    "types",
    "type",
    "call",
    "get",
    "run",
    "with",
    "=",
    "+", "-", "**", "*", "/",
    "%",
    "(", ")", "{", "}", ".", ",", "#", "'", "|", "[", "]",
    ";",
    r#""([^"\\]|\\.)*""#,
    r"[a-zA-Z_][a-zA-Z0-9_]*",
    r"-?[0-9]+",
    r"[ \t\r\f]+" => { },
    r"//[^\n]*" => { },
    "\n",
    r"<[a-zA-Z0-9_]+>",
}

pub Program: Vec<Stmt> = Stmt*;

Stmt: Stmt = {
    Sep* "let" <ls:LetStmt> Sep => ls,
    Sep* "print" <s:StringLit> <e:Expr> Sep => Stmt::PrintTemplate(s, Some(e)),
    Sep* "print" <e:Expr> Sep => Stmt::PrintExpr(e),
    Sep* "return" <e:Expr?> Sep => Stmt::Return(e),
    Sep* "list" "types" Sep => Stmt::ListTypes,
    Sep* "list" "type" <name:Ident> Sep => Stmt::ListType(name),
    Sep* "call" <t:Ident> "." <op:Ident> "(" ")" Sep => Stmt::Call(t, op, vec![]),
    Sep* "call" <t:Ident> "." <op:Ident> "(" <args:ArgNonEmpty> ")" Sep => Stmt::Call(t, op, args),
    Sep* "reg" "operator" <op:CustomOp> <fname:Ident> Sep => Stmt::RegOp(op, fname),
    Sep* "run" "(" ")" Sep => Stmt::RunList(vec![]),
    Sep* "run" "(" <items:ArgNonEmpty> ")" Sep => Stmt::RunList(items),
    Sep* <e:Expr> Sep => Stmt::ExprStmt(e),
    Sep* <name:Ident> "{" <b:Stmt*> "}" Sep => Stmt::Block(name, vec![], b),
    Sep* <name:Ident> "with" "(" <meta:CommaList> ")" "{" <b:Stmt*> "}" Sep => Stmt::Block(name, meta, b),
    Sep* "get" <lst:Expr> <k:Ident> Sep => Stmt::PrintExpr(Expr::Index(Box::new(lst), Box::new(Expr::Str(k)))),
    Sep* "get" <lst:Expr> <k:StringLit> Sep => Stmt::PrintExpr(Expr::Index(Box::new(lst), Box::new(Expr::Str(k)))),
    Sep* "get" <lst:Expr> "'" <k:Ident> Sep => Stmt::PrintExpr(Expr::Index(Box::new(lst), Box::new(Expr::Str(k)))),
    Sep* "get" <lst:Expr> <i:Number> Sep => Stmt::PrintExpr(Expr::Index(Box::new(lst), Box::new(Expr::Int(i)))),
};

LetStmt: Stmt = {
    <name:Ident> "(" ")" "=" <body:Expr> => Stmt::LetFunc(name, vec![], vec![Stmt::Return(Some(body))]),
    <name:Ident> "(" <ps:ParamNonEmpty> ")" "=" <body:Expr> => Stmt::LetFunc(name, ps, vec![Stmt::Return(Some(body))]),
    <name:Ident> "(" ")" "=" "{" <b:Stmt*> "}" => {
          Stmt::LetFunc(name, vec![], b)
     },
     <name:Ident> "(" <ps:ParamNonEmpty> ")" "=" "{" <b:Stmt*> "}" => {
          Stmt::LetFunc(name, ps, b)
     },
    <name:Ident> "=" <e:Expr> => Stmt::Let(name, e),
};

/* removed Block to avoid LR(1) ambiguity */

ArgNonEmpty: Vec<Expr> = {
    <e:Expr> => vec![e],
    <l:ArgNonEmpty> "," <e:Expr> => { l.into_iter().chain(std::iter::once(e)).collect() },
};

ParamNonEmpty: Vec<String> = {
    <p:Ident> => vec![p],
    <l:ParamNonEmpty> "," <p:Ident> => { l.into_iter().chain(std::iter::once(p)).collect() },
};

CustomOp: String = r"<[a-zA-Z0-9_]+>" => {
    let s = <>;
    s[1..s.len()-1].to_string()
};

Sep: () = { ";" => (), "\n" => () };

Expr: Expr = AddSub;

AddSub: Expr = {
    <l:AddSub> "+" <r:Term> => Expr::Add(Box::new(l), Box::new(r)),
    <l:AddSub> "-" <r:Term> => Expr::Sub(Box::new(l), Box::new(r)),
    <l:AddSub> <op:CustomOp> <r:Term> => Expr::Custom(Box::new(l), op, Box::new(r)),
    <l:AddSub> "|" <f:Ident> => Expr::Map(Box::new(l), f),
    Term,
};

Term: Expr = {
    <l:Term> "*" <r:Power> => Expr::Mul(Box::new(l), Box::new(r)),
    <l:Term> "/" <r:Power> => Expr::Div(Box::new(l), Box::new(r)),
    <l:Term> "%" <r:Power> => Expr::Mod(Box::new(l), Box::new(r)),
    Power,
};

Power: Expr = {
    <l:Base> "**" <r:Power> => Expr::Pow(Box::new(l), Box::new(r)),
    Base,
};

Base: Expr = {
    Postfix,
    Atom,
};


CommaTail: Vec<ListItem> = {
    <e:ListItem> => vec![e],
    <l:CommaTail> "," <e:ListItem> => { l.into_iter().chain(std::iter::once(e)).collect() },
};

CommaList: Vec<ListItem> = {
    <e:ListItem> "," <t:CommaTail> => { std::iter::once(e).chain(t.into_iter()).collect() },
};

ListItem: ListItem = {
    <k:Ident> "=" <v:Expr> => ListItem{ key: Some(k), value: v },
    <k:StringLit> "=" <v:Expr> => ListItem{ key: Some(k), value: v },
    <v:Expr> => ListItem{ key: None, value: v },
};

Postfix: Expr = {
    <p:Postfix> "[" <i:Expr> "]" => Expr::Index(Box::new(p), Box::new(i)),
    <f:Ident> "(" ")" => Expr::Call(f, vec![]),
    <f:Ident> "(" <args:ArgNonEmpty> ")" => Expr::Call(f, args),
    <v:Ident> => Expr::Var(v),
};

Atom: Expr = {
    <n:Number> => Expr::Int(n),
    <s:StringLit> => Expr::Str(s),
    "let" => Expr::Var("let".to_string()),
    "#" <s:StringLit> => Expr::Sym(s),
    "#" <v:Ident> => Expr::Str(v),
    "'" <v:Ident> => Expr::Str(v),
    "(" ")" => Expr::List(vec![]),
    "(" <items:CommaList> ")" => Expr::List(items),
    "(" <first:Expr> ")" => Expr::Group(Box::new(first)),
    "[" "]" => Expr::Call("value_of".to_string(), vec![Expr::List(vec![])]),
    "[" <item:ListItem> "]" => Expr::Call("value_of".to_string(), vec![Expr::List(vec![item])]),
    "[" <items:CommaList> "]" => Expr::Call("value_of".to_string(), vec![Expr::List(items)]),
};

Ident: String = r"[a-zA-Z_][a-zA-Z0-9_]*" => <>.to_string();
Number: i64 = r"-?[0-9]+" => <>.parse().unwrap();
StringLit: String = r#""([^"\\]|\\.)*""# => {
    let s = <>;
    s[1..s.len()-1].to_string()
};
